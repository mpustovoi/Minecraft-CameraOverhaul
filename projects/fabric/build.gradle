plugins {
	id 'fabric-loom' version '1.6-SNAPSHOT' apply false
}

subprojects {
	apply plugin: 'fabric-loom'

	repositories {
		maven { url "https://maven.shedaniel.me" }
		maven { url "https://maven.terraformersmc.com/releases" }
	}

	def clothConfigMajor = project.cloth_config_version.split("\\.")[0].toInteger()

	dependencies {
		// To change the versions see the gradle.properties file
		minecraft "com.mojang:minecraft:${project.minecraft_version}"
		mappings loom.officialMojangMappings()
		modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

		// Include JOML for its vectors.
		include (implementation "org.joml:joml:1.10.5")

		// Cloth Config
			// include "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"
		modApi("me.shedaniel.cloth:${clothConfigMajor <= 2 ? "config-2" : "cloth-config-fabric"}:${project.cloth_config_version}") {
			// Prevent preparing two loader versions in cache. Not needed.
			exclude(group: "net.fabricmc")
			exclude(group: "net.fabricmc.fabric-api")
		}

		// ModMenu API, to add the Config Screen to it
		modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
	}

	loom {
		mods {
			"${project.properties.mod_id}" {
				sourceSet sourceSets.main
			}
		}
	}

	processResources {
		inputs.property "version", project.version

		filesMatching("fabric.mod.json") {
			expand "version": project.version
		}
	}
}