plugins {
	id "fabric-loom" version "1.8-SNAPSHOT"
}

base {
	archivesName = property "archives_base_name"
	group = property "maven_group"
	//version = property("mod.version")
	version = "v${property "mod.version"}-${property "loader.id"}+mc${property "mc.displayed_range"}"
}

repositories {
	maven { url "https://maven.shedaniel.me" }
	maven { url "https://maven.terraformersmc.com/releases" }
}

def mcVersion = property("mc.version").toString()
def clothConfigVersion = property("mods.clothconfig.ref")
def clothConfigMajor = clothConfigVersion != "[VERSIONED]" ? clothConfigVersion.split("\\.")[0].toInteger() : 0

// To change any versions see the gradle.properties files under root and "/versions/*/"
dependencies {
	minecraft "com.mojang:minecraft:${mcVersion}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${property("deps.fabric_loader")}"

	// Common libraries
	include (implementation "io.hotmoka:toml4j:0.7.3")
	if (stonecutter.eval(mcVersion, "<1.19.3")) {
		include(implementation "org.joml:joml:1.10.5")
	}

	// Cloth Config
	modApi("me.shedaniel.cloth:${clothConfigMajor <= 2 ? "config-2" : "cloth-config-fabric"}:${clothConfigVersion}") {
		// Prevent preparing two loader versions in cache. Not needed.
		exclude(group: "net.fabricmc")
		exclude(group: "net.fabricmc.fabric-api")
	}
	// ModMenu API, to add the Config Screen to it
	modImplementation "com.terraformersmc:modmenu:${property("mods.modmenu.ref")}"
}

def javaVersion = stonecutter.eval(mcVersion, ">=1.20.6") ? JavaVersion.VERSION_21 : JavaVersion.VERSION_17
tasks.withType(JavaCompile).configureEach {
	it.options.release = javaVersion.ordinal() + 1
}

processResources {
	filesMatching("fabric.mod.json") {
		expand([
			"mod_version": project.property("mod.version"),
			"mc_version_range": project.property("mc.version_range"),
			"mods_clothconfig_range": project.property("mods.clothconfig.range"),
			"mods_modmenu_range": project.property("mods.modmenu.range"),
		])
	}
}

loom {
	runConfigs.all {
		ideConfigGenerated true // Run configurations are not created for subprojects by default
		runDir "../../run" // Use a shared run folder and create separate worlds
	}
}

// Copy produced jars into /out/
task copyJar(type: Copy) {
	from remapJar into "../../out/"
}
build.finalizedBy copyJar
